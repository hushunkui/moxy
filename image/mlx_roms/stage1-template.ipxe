#!ipxe

# Global settings.
# TODO(soltesz): how to make this address configurable?
set epoxyaddress boot-api-dot-mlab-staging.appspot.com

set menu_timeout_ms:int32 5000
set fetch_timeout_ms 10000

set retry_delay_s:int32 30
set max_retry_delay_s 480

# Setup static network configuration.
ifopen net0
set net0/ip {{ip}}
set net0/gateway {{gateway}}
set net0/netmask {{netmask}}
set net0/dns {{dns1}}
set hostname {{hostname}}

# The ePoxy nextboot URL.
set nextboot_url https://${epoxyaddress}/v1/boot/${hostname}/stage2.ipxe

########################################
# Boot menu.
:startmenu
  # Double menu delay for every retry.
  inc menu_timeout_ms ${menu_timeout_ms}
  menu M-Lab iPXE boot menu: ${epoxyaddress}
    item --gap --   Production options:
    item nextboot   -- Boot nextboot script
    item localboot  -- Boot local disk
    item --gap
    item --gap --   Diagnostic options:
    item shell      -- iPXE Shell
    item configure  -- Configure settings
    item reboot     -- Reboot
  choose --timeout ${menu_timeout_ms} boottype

  # Jump to the corresponding menu option.
  goto ${boottype}


########################################
# Diagnostic options.
:shell
  echo Type 'exit' to get back to the menu.
  shell
  goto startmenu

:configure
  echo Starting config.
  config
  goto startmenu

:reboot
  reboot


########################################
# Production options.
:nextboot
  echo Fetching nextboot script.
  goto firstfetch

:retry_loop iseq ${retry_delay_s} ${max_retry_delay_s} && goto fetch_timeout_local_boot ||
  inc retry_delay_s ${retry_delay_s}
  echo Sleeping ${retry_delay_s} seconds...
  sleep ${retry_delay_s}

:firstfetch
  # Setup parameters to send to server.
  params
  param platform     ${platform}      # pcbios / efi.
  param buildarch    ${buildarch}     # i386 / x86_64
  param serial       ${serial}        # system serial number.
  param asset        ${asset}         # system asset tag.
  param uuid         ${uuid}          # system uuid.
  param manufacturer ${manufacturer}  # system manufacturer.
  param product      ${product}       # system product.
  param chip         ${chip}          # NIC chipset.
  param mac          ${mac}           # NIC MAC address.
  param ip           ${ip}            # IP address.
  param version      ${version}       # iPXE version.

  imgfetch --name nextboot.ipxe --timeout ${fetch_timeout_ms} ${nextboot_url}##params || goto retry_loop
  # imgfetch --name nextboot.ipxe --timeout ${fetch_timeout_ms} ${nextboot_url} || goto retry_loop
:loop_done


:verify_image
  echo Verifying and booting nextboot script.
  chain     nextboot.ipxe              || goto verify_error


:verify_error
  echo Failed to chain load nextboot script. Sleeping 5 seconds and starting over.
  sleep 5
  goto startmenu


:localboot
:fetch_timeout_local_boot
  echo Sleeping 10 seconds and booting from local hard drive.
  sleep 10
  sanboot --no-describe --drive 0x80 || shell
